#!/usr/bin/perl

use strict;
use warnings;

use feature qw/say switch/;

use ParseUtil::Domain ':parse';
binmode( STDOUT, "utf8" );
binmode( STDERR, "utf8" );
binmode( STDIN,  "utf8" );
my @input;
if (@ARGV) {
    @input = @ARGV;
}
else {
    @input = <>;
}

foreach my $domain (@input) {
    chomp $domain;
    my @keys;
    given ($domain) {
        when (/\.?xn--/) {
            @keys = qw/domain zone/;
        }
        default {
            @keys = qw/domain_ace zone_ace/;
        }
    }
    my $parsed;
    local $@;
    eval {
        $parsed = parse_domain($domain);
        say join "." => @{$parsed}{@keys};
    };
    if ( my $e = $@ ) {
        print STDERR "# " . $domain . ":\t" . $e;
    }
}
exit(0);

__END__

=head1 NAME

    convertpuny - Commandline puny en/decoder.

=head1 USAGE

    convertpuny xn--fa-hia.de
    fa√ü.de

=head1 DESCRIPTION

    This just converts domains to and from puny encoding. Useful for
    commandline and or batch operations with text files.


